plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '1.3.0'     // https://github.com/JetBrains/gradle-intellij-plugin
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'   // https://plugins.gradle.org/plugin/org.jetbrains.kotlin.jvm
}

group 'com.vk'
// important! update changeNotes (below) on version bump
version '1.2.2'

repositories {
    mavenCentral()
    maven {
        url 'https://www.jetbrains.com/intellij-repository/snapshots'
    }
}
dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

idea {
    module {
        excludeDirs = [
            file(".idea"),
            file(".gradle"),
            file("gradle"),
            file("build"),
        ]
    }
}

intellij {
    type = 'IU'
    // for release versions: https://www.jetbrains.com/intellij-repository/releases (com.jetbrains.intellij.idea)
    // for EAPs: https://www.jetbrains.com/intellij-repository/snapshots
    version = '2021.3'
    plugins = [
        'com.jetbrains.php:213.5744.223', // https://plugins.jetbrains.com/plugin/6610-php/versions
    ]
}
runIde {
    ideDir.set(file("/Users/alexandr.kirsanov/Library/Application Support/JetBrains/Toolbox/apps/PhpStorm/ch-0/213.5744.279/PhpStorm.app/Contents"))
}

patchPluginXml {
    // see https://github.com/JetBrains/gradle-intellij-plugin#patching-dsl
    // <change-notes> is only for LATEST version and should be updated on every version bump
    changeNotes = """
<ul>
  <li>adapt code for 2021.3</li>
</ul>
"""
}
buildSearchableOptions {
    enabled = false
}

sourceSets {
    test {
        resources {
            srcDir 'src/test/fixtures'
        }
    }
}
test {
    exclude 'com/vk/kphpstorm/testing/infrastructure/**'
    include "**/*Test.class"
    scanForTestClasses false
}
