plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '1.6.0'     // https://github.com/JetBrains/gradle-intellij-plugin
    id 'org.jetbrains.kotlin.jvm' version "1.7.0-RC"   // https://plugins.gradle.org/plugin/org.jetbrains.kotlin.jvm
}

group 'com.vk'
// important! update changeNotes (below) on version bump
version '1.2.3'

repositories {
    mavenCentral()
    maven {
        url 'https://www.jetbrains.com/intellij-repository/snapshots'
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.8.2'
    testCompileOnly 'junit:junit:4.13.2'
}

idea {
    module {
        excludeDirs = [
            file(".idea"),
            file(".gradle"),
            file("gradle"),
            file("build"),
        ]
    }
}

compileKotlin {
    sourceCompatibility = 11
    targetCompatibility = 11
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += '-Xjvm-default=all'
        jvmTarget = '11'
    }
}

wrapper {
    gradleVersion = "7.4"
}

intellij {
    type = 'IU'
    // for release versions: https://www.jetbrains.com/intellij-repository/releases (com.jetbrains.intellij.idea)
    // for EAPs: https://www.jetbrains.com/intellij-repository/snapshots
    version = '222.2680-EAP-CANDIDATE-SNAPSHOT'
//    version = '2022.1.1'
    plugins = [
        'com.jetbrains.php:222.2680.8', // https://plugins.jetbrains.com/plugin/6610-php/versions
//        'com.jetbrains.php:221.5591.58',
    ]
}
runIde {
//    ideDir.set(file("/Users/alexandr.kirsanov/Library/Application Support/JetBrains/Toolbox/apps/PhpStorm/ch-0/213.5744.279/PhpStorm.app/Contents"))
}

patchPluginXml {
    // see https://github.com/JetBrains/gradle-intellij-plugin#patching-dsl
    // <change-notes> is only for LATEST version and should be updated on every version bump
    changeNotes = """
<ul>
  <li>adapt code for 2022.1</li>
</ul>
"""
}
buildSearchableOptions {
    enabled = false
}

sourceSets {
    test {
        resources {
            srcDir 'src/test/fixtures'
        }
    }
}
test {
    useJUnitPlatform()

    exclude 'com/vk/kphpstorm/testing/infrastructure/**'
    include "**/*Test.class"
    scanForTestClasses false
}
